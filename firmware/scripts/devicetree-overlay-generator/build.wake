tuple DevicetreeOverlayGeneratorOptions =
  global TopDTSFile:    Path
  global OtherDTSFiles: List Path
  global Type:          String
  global OutputFile:    String

# Instead of copying the script sources into build/{here}, we'll just
# execute directly out of the package directory, since nothing gets
# modified during execution.
#
# The virtualenv does get placed in build/{here} by
# addPythonRequirementsEnv, along with a copy of requirements.txt
def generatorDir = here

#######################################################################
# makeDevicetreeOverlayGeneratorOptions takes the following parameters:
#   - topDTSFile: The top-level Devicetree source file
#   - otherDTSFiles: any other Devicetree source files included in the
#                    hierarchy of Devicetree source files
#   - type: One of {rtl, arty, qemu, hifive, spike, vc707}
#   - outputFile: A string representing the path of the output file to
#                 produce
#######################################################################
global def makeDevicetreeOverlayGeneratorOptions topDTSFile otherDTSFiles type_ outputFile =
  DevicetreeOverlayGeneratorOptions topDTSFile otherDTSFiles type_ outputFile

global def runDevicetreeOverlayGenerator options =
  def topDTSFile = options.getDevicetreeOverlayGeneratorOptionsTopDTSFile
  def otherDTSFiles = options.getDevicetreeOverlayGeneratorOptionsOtherDTSFiles

  def inputs =
    def generatorSources = sources here `.*\.py`
    def dtsSources = topDTSFile, otherDTSFiles
    generatorSources ++ dtsSources

  def outputs = options.getDevicetreeOverlayGeneratorOptionsOutputFile, Nil

  def args =
    def output =
      "-o", options.getDevicetreeOverlayGeneratorOptionsOutputFile,
      Nil

    def type_ =
      "-t", options.getDevicetreeOverlayGeneratorOptionsType,
      Nil

    # The generated overlay will include an `/include/` directive at the top of
    # the file which points at the topDTSFile. To make sure that that directive
    # includes the filename only and not the fully-qualified path, we can pass
    # the --rename-include option.
    def renameInclude =
      def notdir str =
        extract `.*/(.*)` str | head | getOrElse str
      "--rename-include", topDTSFile.getPathName.notdir,
      Nil

    def dts =
      topDTSFile.getPathName,
      Nil

    output ++ type_ ++ renameInclude ++ dts

  makePlan (pythonCommand "{generatorDir}/generate_overlay.py" args) inputs
  | addPlanRelativePath "PYTHONPATH" generatorDir
  | addPythonRequirementsEnv generatorDir
  | setPlanFnOutputs (\_ outputs)
  | runJob

# This allows the python virtualenv to be created prior to running a build
# with `wake preinstall Unit`.
publish preinstall = (pythonRequirementsInstaller generatorDir), Nil

#########################################################################
# installDevicetreeOverlayGenerator allows wake flows to install the
# overlay generator in customer deliveries and exclude all content which
# does not directly contribute to the generation of customer BSP content.
# It takes the following parameter
#   - installPath: the path to install the generator in
# For example, if you call `installDevicetreeOverlayGenerator "scripts"`
# the generator script will be found in
#   scripts/devicetree-overlay-generator/generate_overlay.py
#########################################################################
global target installDevicetreeOverlayGenerator installPath =
  def generatorSources =
    source "{here}/README.md",
    source "{here}/LICENSE",
    source "{here}/generate_overlay.py",
    sources "{here}/targets" `.*\.py`

  mkdir installPath,

  map (installIn installPath) generatorSources
